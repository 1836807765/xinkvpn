//
// Configure ecj as java compiler
//
configurations {
    ecj
}

dependencies {
    ecj 'org.eclipse.jdt.core.compiler:ecj:3.7.1'
}

// ecj warning flags
def warnings = [
    'allOver-ann',
    'conditionAssign',
    'hashCode',
    'hiding',
    'emptyBlock',
    'null',
    'semicolon',
    'unused',
    'uselessTypeCheck',
]

compileJava {
    options.fork = true
    options.define compilerArgs: [
        '-encoding', 'utf-8',
        '-source', sourceCompatibility,
        '-target', targetCompatibility,
        "-warn:+${warnings.join(',')}"
    ]

    doFirst {
        options.fork executable: 'java', jvmArgs: [ '-cp', configurations.ecj.asPath, 'org.eclipse.jdt.internal.compiler.batch.Main' ]
    }
}
